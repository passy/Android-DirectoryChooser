apply plugin: 'android-library'
apply plugin: 'android-test'
apply plugin: 'checkstyle'

repositories {
  maven {
    url 'https://oss.sonatype.org/content/repositories/snapshots/'
  }
}

android {
  compileSdkVersion 19
  buildToolsVersion '19.0.1'

  defaultConfig {
    minSdkVersion 11
    targetSdkVersion 18
  }

  dependencies {
    compile 'com.google.code.findbugs:jsr305:2.0.2'

    testCompile 'junit:junit:4.10'
    testCompile 'org.robolectric:robolectric:2.3-SNAPSHOT'
    testCompile 'com.squareup:fest-android:1.0.+'
    testCompile 'org.mockito:mockito-all:1.9.+'
  }

  lintOptions {
    abortOnError true
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}

checkstyle {
  configFile project.file('../config/checkstyle/checkstyle.xml')
  showViolations true
}

apply from: 'https://raw.github.com/chrisbanes/gradle-mvn-push/eaa6b5404b7594e6c23b884fdc5795f545db55dd/gradle-mvn-push.gradle'

import org.gradle.plugins.signing.Sign

gradle.taskGraph.whenReady { taskGraph ->
  if (taskGraph.allTasks.any { it instanceof Sign } &&
      'uploadArchives' in gradle.startParameter.taskNames) {
    // Use Java 6's console to read from the console (no good for a CI environment)
    Console console = System.console()
    console.printf "\n\nWe have to sign some things in this build.\n\nPlease enter your signing details.\n\n"
    allprojects { ext."signing.password" = console.readPassword("PGP Private Key Password: ") }
  }
}
